100 CLS:PRINTCHR$(23)
110 RANDOM:DEFINTA-Z:CLEAR500
120 PRINT@14,"FOREST FIRE"
130 PRINT@204,"SKILL LEVELS:"
140 PRINT@334,"1-IMPOSSIBLE"
150 PRINT@398,"2-PRO"
160 PRINT@462,"3-AVERAGE"
170 PRINT@526,"4-BEGINNER":PRINT
180 INPUT"WHAT SKILL LEVEL DO YOU WANT";S
190 IFS<1THEN180
200 ' A$=MAP FI=IGNITION FH=HEAT FT=BURN TIME
210 ' FX/FY=FIRE POSITION FP=PREVIOUS FF=FOLLOWING
220 DIMA$(16,16),FI(16,16),FH(16,16),FT(16,16):DIMFX(225),FY(225),FP(225),FF(225)
230 S$=" ":C$="F":T$="#":L$="L":F$="*":X$="X":M$="+":W$="W"
240 DIMDX(8):DX(1)=-1:DX(2)=0:DX(3)=1:DX(4)=-1:DX(5)=1:DX(6)=-1:DX(7)=0:DX(8)=1
250 DIMDY(8):DY(1)=-1:DY(2)=-1:DY(3)=-1:DY(4)=0:DY(5)=0:DY(6)=1:DY(7)=1:DY(8)=1
280 CLS:PRINTCHR$(23)
290 FORI=64TO960STEP64:PRINT@I,"/";:NEXT
300 PRINT@4,"/ / / / FOREST FIRE / / / / /";
310 FORI=1TO1+RND(2)
320 X3=RND(11)+2:Y3=RND(11)+2
330 X1=X3-RND(3)+1:X2=X3+RND(3)-1
340 Y1=Y3-RND(3)+1:Y2=Y3+RND(3)-1
350 PRINT@4,"/ / / / /   FARM  / / / / / /";
360 FORX=X1TOX2:FORY=Y1TOY2
370 IFA$(X,Y)<>C$THENNC=NC+1
380 A$(X,Y)=C$:GOSUB9100:FI(X,Y)=-1:FH(X,Y)=-1
390 NEXTY,X,I
400 X3=RND(11)+2:Y3=RND(11)+2
410 Z=RND(2):X1=X3-Z:X2=X3+Z
420 PRINT@4,"/ / / / /   LAKE  / / / / / /";
430 FORX=X1TOX2
440 Z1=Z-ABS(X-X3)
450 Y1=Y3-Z1:Y2=Y3+Z1
460 FORY=Y1TOY2
470 IFA$(X,Y)=C$THENNC=NC-1
480 NL=NL+1:A$(X,Y)=L$:GOSUB9100:FI(X,Y)=999
490 NEXTY,X
500 X3=RND(9)+3:Y3=RND(9)+2
510 Z=RND(2):Y1=Y3-Z:Y2=Y3+Z
520 PRINT@4,"/ / / / /   TOWN  / / / / / /";
530 FORY=Y1TOY2
540 Z1=Z-ABS(Y-Y3)
550 X1=X3+RND(3)-2-Z1:X2=X3+RND(3)-2+Z1
560 FORX=X1TOX2
570 IFA$(X,Y)=C$THENNC=NC-1:GOTO600
580 IFA$(X,Y)=L$THENNL=NL-1
600 NT=NT+1:A$(X,Y)=T$:GOSUB9100:FI(X,Y)=1
610 NEXTX,Y
650 PX=X3:PY=Y3
700 PRINT@4,"/ / / / /   FIRE  / / / / / /";
710 X=RND(13)+1:Y=RND(13)+1
720 IFA$(X,Y)<>""ORABS(X-PX)+ABS(Y-PY)<8THEN710
730 ' FS/FE=INDEX TO FIRST/LAST FIRE
740 FS=1:FE=1:FX(FS)=X:FY(FS)=Y:A$(X,Y)=F$:GOSUB9100
800 PRINT@4,"/ / / / FOREST FIRE / / / / /";
900 NF=225-NC-NL-NT:EF=NF-1:EC=NC:EL=NL:ET=NT
950 P$=M$:GOSUB9200
1000 ' BURN OUT
1010 I=FS
1020 IFC<4THENGOTO1050
1030 C=0:GOSUB5000
1040 IFE=1THEN2000
1050 C=C+1
1070 X=FX(I):Y=FY(I):FT(X,Y)=FT(X,Y)+1
1080 IFA$(X,Y)=F$ANDFT(X,Y)<2THEN1160
1090 A$(X,Y)="":FX(I)=0:FY(I)=0:GOSUB9000
1100 ' REMOVE FIRE FROM LIST/END GAME
1110 IFI=FSANDI=FETHEN2000
1120 IFI=FSTHENFS=FF(I):GOTO1140
1130 FF(FP(I))=FF(I)
1140 IFI=FETHENFE=FP(I):GOTO1200
1150 FP(FF(I))=FP(I)
1160 IFI<>FETHENI=FF(I):GOTO1020
1200 ' APPLY HEAT TO NEIGHBOURING CELLS
1210 I=FS:NE=FE
1220 IFC<1THENGOTO1250
1230 C=0:GOSUB5000
1240 IFE=1THEN2000
1250 C=C+1
1300 H=FH(FX(I),FY(I))
1310 FORJ=1TO8
1320 X=FX(I)+DX(J):Y=FY(I)+DY(J)
1330 IFFT(X,Y)>1THEN1490
1340 IFA$(X,Y)=F$THEN1490
1350 IFX<1ORX>15ORY<1ORY>15THEN1490
1360 IFH<FI(X,Y)+S+S-RND(6)-3THEN1490
1400 ' START FIRE
1410 FP=NE:NE=NE+1:IFNE<=225ANDFX(NE)=0THEN1450
1420 NE=1
1430 IFFX(NE)<>0THENNE=NE+1:GOTO1430
1450 FP(NE)=FP:FF(FP)=NE:FF(NE)=0
1460 GOSUB9300
1470 A$(X,Y)=F$:GOSUB9000
1480 FX(NE)=X:FY(NE)=Y
1490 NEXTJ
1500 IFI<>FETHENI=FF(I):GOTO1220
1510 FE=NE
1520 GOTO1000
2000 LT=NT-ET:LC=NC-EC:LF=NF-EF
2010 CLS:PRINTCHR$(23)
2020 PRINT"FINAL REPORT---"
2030 PRINT
2040 PRINTLT+LC+LF;"ACRES OF LAND"
2050 PRINT" HAVE BEEN LOST"
2060 PRINT"INCLUDING:"
2070 PRINTLT;"ACRES OF THE TOWN,"
2080 PRINTLC;"ACRES OF FARMLAND, AND"
2090 PRINTLF;"ACRES OF FOREST."
2100 PRINT"YOU MANAGED TO SAVE"
2110 PRINTET;"ACRES OF THE TOWN,"
2120 PRINTEC;"ACRES OF FARMLAND, AND"
2130 PRINTEF;"ACRES OF FOREST."
2140 PRINT
2150 SC=INT(200*ET/NT+50*EC/(NC+1)+10*EF/NF)
2160 PRINT"YOUR SCORE IS:"SC
2170 INPUT"DO YOU WANT TO TRY AGAIN";A$
2180 IFLEFT$(A$,1)<>"N"THENRUN
2190 END
5000 A$=INKEY$:IFA$=""THENRETURNELSEAA$=A$
5010 X=PX:Y=PY
5020 IFAA$<>CHR$(8)GOTO5100
5030 IFPY=1THENRETURN
5040 PY=PY-1:GOSUB9100:GOSUB9200:RETURN
5100 IFAA$<>CHR$(9)GOTO5200
5110 IFPY=15THENRETURN
5120 PY=PY+1:GOSUB9100:GOSUB9200:RETURN
5200 IFAA$<>CHR$(10)GOTO5300
5210 IFPX=15THENRETURN
5220 PX=PX+1:GOSUB9100:GOSUB9200:RETURN
5300 IFAA$<>CHR$(91)GOTO5400
5310 IFPX=1THENRETURN
5320 PX=PX-1:GOSUB9100:GOSUB9200:RETURN
5400 IFAA$<>"X"GOTO5500
5410 IFA$(PX,PY)=F$ORA$(PX,PY)=L$ORA$(PX,PY)=X$THENRETURN
5420 A$(PX,PY)=X$:FI(PX,PY)=999
5430 X=PX:Y=PY:GOSUB9100:GOSUB9300:RETURN
5500 IFAA$<>"W"GOTO5700
5510 IFP$=W$THEN5600
5520 IFA$(PX,PY)=L$THENP$=W$:GOSUB9200
5530 RETURN
5600 FORX=PX-1TOPX+1:FORY=PY-1TOPY+1
5610 IFA$(X,Y)<>F$GOTO5650
5620 A$(X,Y)=""
5630 IFPX<>XORPY<>YTHENPRINT@X*64+Y*4," ";
5650 FI(X,Y)=FI(X,Y)+1:FH(X,Y)=FH(X,Y)-1
5660 NEXTY,X
5670 P$=M$:GOSUB9200:RETURN
5700 IFAA$<>"E"THENRETURN
5710 E=1:RETURN
9000 IFX=PXANDY=PYTHENRETURN
9100 Z$=A$(X,Y):IFZ$=""THENZ$=S$
9110 PRINT@X*64+Y*4,Z$;:RETURN
9200 PRINT@PX*64+PY*4,P$;:RETURN
9300 IFA$(X,Y)=""THENEF=EF-1:RETURN
9310 IFA$(X,Y)=C$THENEC=EC-1:RETURN
9320 IFA$(X,Y)=T$THENET=ET-1:RETURN
9330 RETURN
